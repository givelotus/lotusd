# Maintainer: Josh Ellithorpe <quest@mac.com>

pkgname=lotusd
pkgver=7.3.3
pkgrel=0
pkgdesc="Lotus with lotusd, lotus-tx, lotus-seeder and lotus-cli"
arch=('i686' 'x86_64')
url="https://givelotus.org"
depends=('boost-libs' 'libevent' 'openssl' 'zeromq' 'miniupnpc' 'jemalloc')
makedepends=('cmake' 'ninja' 'boost' 'python' 'help2man')
license=('MIT')
source=(https://github.com/LogosFoundation/lotusd/archive/v$pkgver.tar.gz
        lotus.conf
        lotus.logrotate
        lotus.service
        lotus-reindex.service
        lotus.install)
backup=('etc/lotus/lotus.conf'
        'etc/logrotate.d/lotus')
provides=('lotus-cli' 'lotus-daemon' 'lotus-tx' 'lotus-seeder')
conflicts=('lotus-cli' 'lotus-daemon' 'lotus-tx' 'lotus-seeder')
install=lotus.install

build() {
  cd "$srcdir/${pkgname}-$pkgver"

  msg2 'Building...'
  mkdir -p build
  pushd build

  cmake -GNinja .. \
    -DENABLE_CLANG_TIDY=OFF \
    -DCLIENT_VERSION_IS_RELEASE=ON \
    -DENABLE_REDUCE_EXPORTS=ON \
    -DENABLE_STATIC_LIBSTDCXX=ON \
    -DBUILD_BITCOIN_WALLET=OFF \
    -DBUILD_BITCOIN_QT=OFF \
    -DCMAKE_INSTALL_PREFIX=$pkgdir/usr

  ninja
  popd
}

check() {
  cd "$srcdir/${pkgname}-$pkgver/build"

  msg2 'Testing...'
  ninja check
}

package() {
  cd "$srcdir/${pkgname}-$pkgver"

  msg2 'Installing license...'
  install -Dm 644 COPYING -t "$pkgdir/usr/share/licenses/${pkgname}"

  msg2 'Installing examples...'
  install -Dm644 "contrib/debian/examples/lotus.conf" \
    -t "$pkgdir/usr/share/doc/lotus/examples"

  msg2 'Installing documentation...'
  install -dm 755 "$pkgdir/usr/share/doc/lotus"
  for _doc in \
    $(find doc -maxdepth 1 -type f -name "*.md" -printf '%f\n') \
    release-notes; do
      cp -dpr --no-preserve=ownership "doc/$_doc" \
        "$pkgdir/usr/share/doc/lotus/$_doc"
  done

  msg2 'Installing essential directories'
  install -dm 700 "$pkgdir/etc/lotus"
  install -dm 755 "$pkgdir/srv/lotus"
  install -dm 755 "$pkgdir/run/lotus"

  pushd build
  msg2 'Installing executables...'
  ninja install/strip

  msg2 'Installing man pages...'
  ninja install-manpages
  popd

  msg2 'Installing lotus.conf...'
  install -Dm 600 "$srcdir/lotus.conf" -t "$pkgdir/etc/lotus"

  msg2 'Installing lotus.service...'
  install -Dm 644 "$srcdir/lotus.service" -t "$pkgdir/usr/lib/systemd/system"
  install -Dm 644 "$srcdir/lotus-reindex.service" \
    -t "$pkgdir/usr/lib/systemd/system"

  msg2 'Installing lotus.logrotate...'
  install -Dm 644 "$srcdir/lotus.logrotate" "$pkgdir/etc/logrotate.d/lotus"

  msg2 'Installing bash completion...'
  for _compl in lotus-cli lotus-tx lotusd; do
    install -Dm 644 "contrib/${_compl}.bash-completion" \
      "$pkgdir/usr/share/bash-completion/completions/$_compl"
  done
}
